plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'

    // Firebase
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'

    // Jetpack Navigation
    id "androidx.navigation.safeargs.kotlin"
}

def appVersionCode = 112    // NOTICE that version code and name may be DIFFERENT
def appVersionName = "0.1.12"

def applicationIdAlcon = "ru.alconmanagementapp"
def applicationIdOther = "ru.mancomapp"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "$applicationIdAlcon"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false

            // TODO: disable Crashlytics in debug, when required
            manifestPlaceholders = [enableCrashReporting:"true"]
        }
        release {
            minifyEnabled true
            shrinkResources true
            manifestPlaceholders = [enableCrashReporting:"true"]
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def SEP = "_"
            def date = new Date()
            def formattedDate = date.format('dd.MM.YYYY_HH-mm')

            outputFileName = variant.baseName + SEP + appVersionName + SEP + formattedDate + ".apk"
        }
    }

    flavorDimensions "target"
    productFlavors {
        alcon {
            dimension "target"
            applicationId "$applicationIdAlcon"
        }
        other { // dummy flavor
            dimension "target"
            applicationId "$applicationIdOther"
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Android Remote Debugger
    // Do not upgrade (on 2021.03.03 version 1.1.0 breaks the build)
    implementation 'com.github.zerobranch:android-remote-debugger:1.0.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.6.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"

    // Preference
    implementation 'androidx.preference:preference-ktx:1.1.1'

    // Jetpack Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    // Material
    implementation 'com.google.android.material:material:1.3.0'

    // Lottie
    implementation "com.airbnb.android:lottie:3.6.1"

    // Testing
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.mockito:mockito-core:3.8.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.3'
}